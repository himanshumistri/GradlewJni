import org.gradle.internal.jvm.Jvm

/*
 * There is currently no "C application" plugin, so this build uses the "C++ application" plugin and then reconfigures it
 * to build C instead.
 */
plugins {
    id 'cpp-library'
}

group 'org.example'
version '1.0-SNAPSHOT'

library {
    targetMachines.add(machines.macOS) // Ensure support for macOS aarch64

    binaries.configureEach { CppBinary binary ->
        def compileTask = binary.compileTask.get()
        compileTask.includes.from("${Jvm.current().javaHome}/include")

        def osFamily = binary.targetPlatform.targetMachine.operatingSystemFamily
        if (osFamily.macOs) {
            compileTask.includes.from("${Jvm.current().javaHome}/include/darwin")
        } else if (osFamily.linux) {
            compileTask.includes.from("${Jvm.current().javaHome}/include/linux")
        } else if (osFamily.windows) {
            compileTask.includes.from("${Jvm.current().javaHome}/include/win32")
        }

        compileTask.source.from fileTree(dir: "src/main/c", include: "**/*.c")

        def toolChain = binary.toolChain
        if (toolChain instanceof GccCompatibleToolChain) {
            compileTask.compilerArgs.addAll(["-x", "c", "-std=c11"])
        } else if (toolChain instanceof Clang) {
            compileTask.compilerArgs.addAll(["-x", "c", "-std=c11", "-target", "arm64-apple-macos11"]) // Ensure ARM (aarch64) support
        } else if (toolChain instanceof VisualCpp) {
            compileTask.compilerArgs.addAll(["/TC"])
        }

    }
}
